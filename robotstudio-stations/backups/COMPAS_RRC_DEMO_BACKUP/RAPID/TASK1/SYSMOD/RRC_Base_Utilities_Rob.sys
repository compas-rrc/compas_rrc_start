MODULE RRC_Base_Utilities_Rob(SYSMODULE)
    !***********************************************************************************
    !
    ! ETH Zurich / Robotic Fabrication Lab
    ! HIB C 13 / Stefano-Franscini-Platz 1
    ! CH-8093 Zurich
    !
    !***********************************************************************************
    !
    ! PROJECT     :  RRC - Rapid Robot Communication
    !
    ! FUNCTION    :  Base Utilities Library  
    !
    ! AUTHOR      :  Philippe Fleischmann
    !
    ! EMAIL       :  rrc@arch.ethz.ch
    !
    ! HISTORY     :  2019.02.22
    !
    ! Copyright   :  ETH Zurich (CH) 2019
    !                - Philippe Fleischmann
    !                - Michael Lyrenmann
    !                - Gonzalo Casas
    !
    ! License     :  This code base is governed by an EULA (End-User License Agreement)
    !                which you have received together with the software.
    !                You are not allowed to use the software on a real robots
    !                without a signed EULA.
    !
    !                A copy of the EULA content is in the controller task (T_CTRL).
    !
    !***********************************************************************************

    !************************************************
    ! Function    :     Read and set jointtarget
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_RasJointtarget()
        !
        ! Read and set robot axis
        jp_RRC_Act.robax.rax_1:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V1;
        jp_RRC_Act.robax.rax_2:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V2;
        jp_RRC_Act.robax.rax_3:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V3;
        jp_RRC_Act.robax.rax_4:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V4;
        jp_RRC_Act.robax.rax_5:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V5;
        jp_RRC_Act.robax.rax_6:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V6;
        !
        ! Read and set external axis
        jp_RRC_Act.extax.eax_a:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V7;
        jp_RRC_Act.extax.eax_b:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V8;
        jp_RRC_Act.extax.eax_c:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V9;
        jp_RRC_Act.extax.eax_d:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V10;
        jp_RRC_Act.extax.eax_e:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V11;
        jp_RRC_Act.extax.eax_f:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V12;
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC

    !************************************************
    ! Function    :     Read and set robtarget
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_RasRobtarget(\string stMoveType)
        !
        ! Read current data from receiver buffer and set translation 
        p_RRC_Act.trans.x:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V1;
        p_RRC_Act.trans.y:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V2;
        p_RRC_Act.trans.z:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V3;
        !
        ! Read current data from receiver buffer and set rotation (Quaternions) 
        p_RRC_Act.rot.q1:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V4;
        p_RRC_Act.rot.q2:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V5;
        p_RRC_Act.rot.q3:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V6;
        p_RRC_Act.rot.q4:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V7;
        !
        ! Set externeal axis
        IF stMoveType="FrameL" OR stMoveType="FrameJ" THEN
            !
            ! Read current joints 
            jp_RRC_Act:=CJointT();
            !
            ! Use current external values 
            p_RRC_Act.extax:=jp_RRC_Act.extax;
        ELSE
            !
            ! Read current data from receiver buffer
            p_RRC_Act.extax.eax_a:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V8;
            p_RRC_Act.extax.eax_b:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V9;
            p_RRC_Act.extax.eax_c:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V10;
            p_RRC_Act.extax.eax_d:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V11;
            p_RRC_Act.extax.eax_e:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V12;
            p_RRC_Act.extax.eax_f:=bm_RRC_RecBufferRob{n_RRC_ChaNr,n_RRC_ReadPtrRecBuf}.Data.V13;
        ENDIF
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC

    !************************************************
    ! Function    :     Update tool center point speed
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_RasTCPSpeed(num nTCPSpeed)
        VAR num nSpeed;
        !
        ! Round input
        nSpeed:=Round(nTCPSpeed\Dec:=2);
        !
        ! Set speed data
        v_RRC_Act:=v_RRC_Default;
        !
        ! Sanity check       
        IF nSpeed>0 THEN
            !
            ! Good set TCP speed
            v_RRC_Act.v_tcp:=nSpeed;
        ELSE
            !
            ! Not good inform user
            !
            ! Load strings for user information
            st_RRC_Header:="Update Speed";
            st_RRC_ActMsg1:="Requested speed is not posible, minimal value is 0.1 mm/s";
            st_RRC_ActMsg2:="Submited speed : "+NumToStr(nSpeed,1)+" mm/s";
            st_RRC_ActBtn1:="50 mm/s";
            st_RRC_ActBtn2:="100 mm/s";
            st_RRC_ActBtn3:="Set my Speed";
            !
            ! Show user window 
            st_RRC_UIAnswer:=f_RRC_UIMsg(st_RRC_Header,st_RRC_ActMsg1\stMsgL2:=st_RRC_ActMsg2,st_RRC_ActBtn1\stBtn2:=st_RRC_ActBtn2,\stBtn3:=st_RRC_ActBtn3,iconWarning);
            !
            ! Handle user answer
            TEST st_RRC_UIAnswer
            CASE st_RRC_ActBtn1:
                !
                ! Set default value 1
                v_RRC_Act.v_tcp:=v50.v_tcp;
            CASE st_RRC_ActBtn2:
                ! 
                ! Set default value 2 
                v_RRC_Act.v_tcp:=v100.v_tcp;
            CASE st_RRC_ActBtn3:
                !
                ! User interface to set my speed
                v_RRC_Act.v_tcp:=f_RRC_UINumEntry(st_RRC_Header,"Please enter your TCP speed.",0.1,1000,iconInfo);
            DEFAULT:
                !
                ! Program error
                r_RRC_ProgError;
            ENDTEST
        ENDIF
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC

    !************************************************
    ! Function    :     Read and set zone
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_RasZone(num nZone)
        !
        ! Set zone data
        TEST nZone
        CASE -1:
            !
            ! Fine point
            z_RRC_Act:=fine;
        CASE 0:
            !
            ! z0
            z_RRC_Act:=z0;
        CASE 1:
            !
            ! z1
            z_RRC_Act:=z1;
        CASE 5:
            !
            ! z5
            z_RRC_Act:=z5;
        CASE 10:
            !
            ! z10
            z_RRC_Act:=z10;
        CASE 15:
            !
            ! z15
            z_RRC_Act:=z15;
        CASE 20:
            !
            ! z20
            z_RRC_Act:=z20;
        CASE 30:
            !
            ! z30
            z_RRC_Act:=z30;
        CASE 40:
            !
            ! z40
            z_RRC_Act:=z40;
        CASE 50:
            !
            ! z50
            z_RRC_Act:=z50;
        CASE 60:
            !
            ! z60
            z_RRC_Act:=z60;
        CASE 80:
            !
            ! z80
            z_RRC_Act:=z80;
        CASE 100:
            !
            ! z100
            z_RRC_Act:=z100;
        CASE 150:
            !
            ! z150
            z_RRC_Act:=z150;
        CASE 200:
            !
            ! z200
            z_RRC_Act:=z200;
        DEFAULT:
            !
            ! Use fine point as default
            z_RRC_Act:=fine;
        ENDTEST
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC

    !************************************************
    ! Function    :     Read actual jointtarget
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_ReadActJointT(\switch FlyBy)
        !
        ! Wait for Robot in position
        IF NOT Present(FlyBy) WaitRob\InPos;
        !
        ! Read current joint position
        jp_RRC_Act:=CJointT();
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC

    !************************************************
    ! Function    :     Read actual robtarget
    ! Programmer  :     Philippe Fleischmann
    ! Date        :     2019.02.22
    !***************** ETH Zurich *******************
    !
    PROC r_RRC_ReadActRobT(\switch FlyBy)
        !
        ! Wait for Robot in position
        IF NOT Present(FlyBy) WaitRob\InPos;
        !
        ! Read current joint position
        p_RRC_Act:=CRobT(\Tool:=t_RRC_Act\WObj:=ob_RRC_Act);
        RETURN ;
    ERROR
        ! Placeholder for Error Code...
    ENDPROC
ENDMODULE